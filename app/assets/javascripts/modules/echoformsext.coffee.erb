#= require proj4
#= require leaflet-1.3.4/leaflet
#= require modules/map/geoutil
#= require modules/map/leaflet-plugins/proj
#= require modules/map/leaflet-plugins/interpolation
#= require modules/map/image_map

do (L, $=jQuery, projectPath=@edsc.map.L.interpolation.projectPath, Proj=@edsc.map.L.Proj, ImageMap=@edsc.map.ImageMap, hasPending=@edsc.util.xhr.hasPending) ->

  class BoundingBox extends  $.echoforms.controls.Grouping
    @selector="group[id*='NWESBoundingBox']"

    constructor: (args...) ->
      @map
      super(args...)

    _scaledLonLatProjection = (scale) ->
      project: (latlng) ->
        new L.Point(latlng.lng * scale, latlng.lat * scale)
      unproject: (point) ->
        new L.LatLng(point.y / scale, point.x / scale);
      bounds: L.bounds([-180 * scale, -90 * scale], [180 * scale, 90 * scale])

    _simpleScaledCrs = (scale) ->
      L.extend({}, L.CRS.Simple, projection: _scaledLonLatProjection(scale))

    _redrawOnMap: (id, el) =>
      if @map? && @map.getContainer().id == id
        @map.eachLayer (layer) =>
          @map.removeLayer(layer) if layer.id == 'bbox_layer'

        north=parseInt(el.find('div.bbox_north input').val())
        west=parseInt(el.find('div.bbox_west input').val())
        east=parseInt(el.find('div.bbox_east input').val())
        south=parseInt(el.find('div.bbox_south input').val())
        bounding_box = L.polygon([[north, west], [north, east], [south, east], [south, west]], {color: "#54F7A3", fillOpacity: 0.4, weight: 1})
        bounding_box.id = 'bbox_layer'
        bounding_box._interpolationFn = 'cartesian'
        bounding_box.addTo(@map)

    _buildMap: (id, el) =>
      $('[id$=-map]').each (i, element) =>
        if id == $(element).prop('id') && $('#' + id).length > 0 && !$('#' + id).hasClass('leaflet-container')
          @map = L.map id,
            minZoom: 0
            maxZoom: 0
            zoom: 0
            center: [0, 0]
            crs: _simpleScaledCrs(1)
            zoomControl: false
            attributionControl: false
          @map.dragging.disable()
          @map.touchZoom.disable()
          @map.doubleClickZoom.disable()
          @map.scrollWheelZoom.disable()
          @map.tap.disable() if (@map.tap)
          url = '<%= image_path('plate_carree_earth.png') %>'
          L.imageOverlay(url, [[-90, -180], [90, 180]]).addTo(@map)

          @_redrawOnMap(id, el)

    buildDom: ->
      result = super()
      children = result.children('.echoforms-children')
      children.find('>div').addClass('bbox_container')
      children.find('input[type="text"]').addClass('coordinate_input')
      children.find('> div:nth-child(1)').addClass('bbox_north')
      children.find('> div:nth-child(2)').addClass('bbox_west')
      children.find('> div:nth-child(3)').addClass('bbox_east')
      children.find('> div:nth-child(4)').addClass('bbox_south')
      children.find('> div.echoforms-help').css('float','left')

      $(document).ready ->
        waitForEchoforms = setInterval (=>
          formRoot = result.closest('.echoforms-control.echoforms-control-ui.echoforms-grouping-control')
          rootControlIndex = formRoot.prop('id').split('-control-')[1]
          mapId = result.prop('id') + rootControlIndex + '-map'
          result.append("<div id='" + mapId + "' class='image-map'/>") if $('#' + mapId).length == 0
          checkbox = formRoot.find('.echoforms-control-checkbox:contains("Enter bounding box")')
          clearInterval(waitForEchoforms) unless hasPending()
          if checkbox.find('input[type="checkbox"]').is(':checked')
            mapId = $('.echoforms-control-checkbox:contains("Enter bounding box")').closest('.echoforms-children').find('[id$=-map]').prop('id')
            el = $('.echoforms-control-checkbox:contains("Enter bounding box")').closest('.echoforms-children').find('[id*=NWESBoundingBox]')
            BoundingBox.prototype._buildMap(mapId, el)
            $('#' + mapId).toggle(el.find('.bbox_container').is(':visible'))
          ), 0

      $('.access-item-options')
        .on 'echoforms:modelchange', '.echoforms-control-checkbox:contains("Enter bounding box")', (event) =>
          mapId = $(event.target).closest('.echoforms-children').find('[id$=-map]').prop('id')
          el = $(event.target).closest('.echoforms-children').find('[id*=NWESBoundingBox]')
          @_buildMap(mapId, el)
          $('#' + mapId).toggle(el.find('.bbox_container').is(':visible'))
        .on 'change', '.coordinate_input', (event) =>
          mapId = $(event.target).closest('[id$="-NWESBoundingBox"]').find('[id$=-map]').prop('id')
          el = $(event.target).closest('[id$="-NWESBoundingBox"]')
          @_redrawOnMap(mapId, el)
      result

  class XYBoxSubsetter extends $.echoforms.controls.Grouping
    @selector: 'group[id=XYBox]'

    buildDom: ->
      result = super()

      values = @_xyBoxValuesFromQuery()
      if values.length > 0
        @_setValuesToXyBox()

        @subsetOption = $('
          <div class="echoforms-control echoforms-typed-control"
            <div class="echoforms-elements">
              <label><input type="checkbox" checked> Subset around my spatial search area</label>
            </div>
          </div>')

        $checkbox = @subsetOption.find('input')
        $checkbox.on 'click change', (e) =>
          @_setValuesToXyBox($checkbox.is(':checked'))

        result.children('.echoforms-children').prepend(@subsetOption)

      # NSIDCs forms set the projection dropdown to be irrelevant despite it being used,
      # I think as a hack for Reverb / WIST's Jaz panel.
      @controls[0].relevantExpr = null
      @controls[0].relevant(true)
      result

    loadFromModel: ->
      $checkbox = @el.find('input[type="checkbox"]')
      if $checkbox.is(':checked')
        @_setValuesToXyBox(true, false)
      else
        super()

    _setValuesToXyBox: (readonly=true, events=true) ->
      values = @_xyBoxValuesFromQuery()
      controls = @controls

      for value, i in values
        control = controls[i]
        control.readonlyExpr = null
        control.inputs().val(value)
        control.inputs().change() if events
        control.readonly(readonly)


    _xyBoxValuesFromQuery: ->
      return @_xyBoxValues if @_xyBoxValues?

      spatial = edsc.page.query.spatial()
      return @_xyBoxValues = [] unless spatial? && spatial.length > 0

      [type, spatial...] = spatial.split(':')
      spatial = (pt.split(',').reverse().map((c) -> parseFloat(c)) for pt in spatial)
      latlngs = (L.latLng(p) for p in spatial)

      llbounds = L.latLngBounds(latlngs)

      # Figure out which hemisphere we're in.  If the search area is near the equator,
      # neither projection works, so we bail
      hemisphere = null
      if llbounds.getSouth() > 20
        hemisphere = 0
      else if llbounds.getNorth() < -20
        hemisphere = 1
      else
        return @_xyBoxValues = []

      # Since we're in a polar projection, we need to account for all 4 corners of bounding boxes
      interpolationStrategy = 'geodetic'
      if type == 'bounding_box'
        box = L.latLngBounds(latlngs)
        latlngs = [box.getNorthEast(), box.getNorthWest(), box.getSouthEast(), box.getSouthWest()]
        interpolationStrategy = 'cartesian'

      projectionSelect = @controls[0]
      projection = projectionSelect.items[hemisphere][1]

      # Pick the right projection
      if projection.indexOf('EASE') > 0
        proj = [Proj.epsg3408, Proj.epsg3409][hemisphere]
      else
        proj = [Proj.epsg3413, Proj.epsg3031][hemisphere]

      proj = proj.projection
      path = projectPath({latLngToLayerPoint: (ll) -> proj.project(ll)}, latlngs, [], interpolationStrategy, 10)

      bounds = L.bounds(path.boundary)

      @_xyBoxValues = [
        projection,
        Math.round(bounds.min.y),
        Math.round(bounds.min.x),
        Math.round(bounds.max.y),
        Math.round(bounds.max.x)
      ]

  $.echoforms.control(XYBoxSubsetter)
  $.echoforms.control(BoundingBox)

  null
